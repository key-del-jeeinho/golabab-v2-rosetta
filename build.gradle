plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'signing'
}

group 'io.github.key-del-jeeinho'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    // https://mvnrepository.com/artifact/org.projectlombok/lombok
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'
}

test {
    useJUnitPlatform()
}

task javadocJar(type: Jar) {
    archiveClassifier.set 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set 'sources'
    from sourceSets.main.allSource
}

signing {
    sign publishing.publications
    //sign configurations.archives
}

javadoc {
    options.addStringOption("locale","ko_KR")
    options.addStringOption("encoding","UTF-8")
    options.addStringOption("charset","UTF-8")
    options.addStringOption("docencoding","UTF-8")
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.key-del-jeeinho'
            artifactId = 'golabab-v2-rosetta-lib'
            version = '1.0.0-RELEASE'

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'RosettaLibrary'
                description = "It's a library that defines communication objects between various services."
                url = 'https://github.com/key-del-jeeinho/golabab-v2-rosetta'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'velocia'
                        name = 'JeeInho'
                        email = 'velocia.developer@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/key-del-jeeinho/golabab-v2-rosetta.git'
                    developerConnection = 'scm:git:ssh://github.com/key-del-jeeinho/golabab-v2-rosetta.git'
                    url = 'https://github.com/key-del-jeeinho/golabab-v2-rosetta/'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username = project.ossrhUsername
                password = project.ossrhPassword
            }
            name = "OSSRH"
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
        }
    }
}